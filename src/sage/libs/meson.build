sirocco = cc.find_library('sirocco', required: false)
# cannot be found via pkg-config
ecl = cc.find_library('ecl')

py.install_sources(
  'all.py',
  'all__sagemath_coxeter3.py',
  'all__sagemath_meataxe.py',
  'all__sagemath_objects.py',
  'all__sagemath_sirocco.py',
  'ecl.pxd',
  'eclsig.h',
  'gmpxx.pxd',
  'iml.pxd',
  'm4ri.pxd',
  'm4rie.pxd',
  'meataxe.pxd',
  subdir : 'sage/libs'
)

extension_data = {
  'ecl' : files('ecl.pyx'),
  'homfly' : files('homfly.pyx'),
  'libecm' : files('libecm.pyx')
}

dependencies = [py_dep, braiding, cysignals, ecl, ecm, gc, gmp, homfly, mtx, sirocco]

if sirocco.found()
    #extension_data['sirocco'] += files('sirocco.pxd') # not yet on conda
    dependencies += [sirocco]
endif

foreach name, pyx : extension_data
    py.extension_module(name,
        sources: pyx,
        subdir: 'sage/libs',
        install: true,
        include_directories: [inc_cpython, inc_rings],
        dependencies: dependencies,
    )
endforeach

extension_data_cpp = {
    'braiding': files('braiding.pyx'),
}

foreach name, pyx : extension_data_cpp
    py.extension_module(name,
        sources: pyx,
        subdir: 'sage/libs',
        install: true,
        override_options : ['cython_language=cpp'],
        include_directories: [inc_cpython, inc_rings],
        dependencies: dependencies,
    )
endforeach

subdir('arb')
subdir('coxeter3')
install_subdir('cremona', install_dir: sage_install_dir / 'libs')
subdir('eclib')
subdir('flint')
subdir('gap')
subdir('giac')
subdir('glpk')
subdir('gmp')
subdir('gsl')
subdir('lcalc')
subdir('linbox')
install_subdir('linkages', install_dir: sage_install_dir / 'libs')
install_subdir('lrcalc', install_dir: sage_install_dir / 'libs')
install_subdir('mpfr', install_dir: sage_install_dir / 'libs')
subdir('mpmath')
install_subdir('mwrank', install_dir: sage_install_dir / 'libs')
subdir('ntl')
subdir('pari')
install_subdir('pynac', install_dir: sage_install_dir / 'libs')
subdir('singular')
subdir('symmetrica')
